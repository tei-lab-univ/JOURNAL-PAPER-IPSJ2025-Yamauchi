//******************************************************************************
//*  Sample-1.lts (for MTSA)
//*  Create Date      : 2021/4/8
//*  Creator Name     : 
//*  MTSA run command : [java -jar -Xmx8G mtsa.jar] メモリサイズに応じて8Gは要変更
//*****************************************************************************/


//*****************************************************************************
// Environment
//*****************************************************************************

/* set variable */
const MaxHeight = 5 //ドローンは前後の数字の高さにしか移動できない
const MaxX = 5 //ドローンは前後の数字の行先にしか移動できない
const MaxY = 5 //ドローンは前後の数字の行先にしか移動できない

range Height = 1..MaxHeight
range X = 1..MaxX
range Y = 1..MaxY
set Direction = {n,s,e,w}

/* set Actions (事象の集合) */
set ControllerbleActions = {battery['low], battery['safe], stop_rec, takeoff, land, up[2..MaxHeight], down[1..MaxHeight-1], move[Direction][X][Y]}


/* Drone モデル */

//電池残量管理
BATTERY = SAFE,
SAFE = (battery['low] -> LOW),
LOW  = (battery['safe] -> SAFE).

//移動管理(こいつを高度flyだけと移動moveだけ分割したい)
ALTITUDE  = (takeoff -> ACTION[1]),
ACTION[h:Height] = (when(h == 1) land-> ALTITUDE
                   |when(h > 1) down[h-1] -> ACTION[h-1]
				   |when(h < MaxHeight) up[h+1] -> ACTION[h+1]).

AREA      = (takeoff -> ACTION[1][1]),
ACTION[x:X][y:Y] = (land-> AREA
　　　　　　　　　　　|when(x > 1)    move['w][x-1][y] -> ACTION[x-1][y]
				   |when(x < MaxX) move['e][x+1][y] -> ACTION[x+1][y]
				   |when(y > 1)    move['s][x][y-1] -> ACTION[x][y-1]
				   |when(y < MaxY) move['n][x][y+1] -> ACTION[x][y+1]).

|| DroneEnvironment = (BATTERY || ALTITUDE || AREA).


//             N
//   ┏━━━┳━━━┳━━━┳━━━┳━━━┓
//   ┃   ┃   ┃   ┃   ┃   ┃
//   ┣━━━╋━━━╋━━━╋━━━╋━━━┫
//   ┃   ┃   ┃   ┃   ┃   ┃
//   ┣━━━╋━━━╋━━━╋━━━╋━━━┫
// W ┃   ┃   ┃   ┃   ┃   ┃ E
//   ┣━━━╋━━━╋━━━╋━━━╋━━━┫
//   ┃   ┃   ┃   ┃   ┃   ┃
//   ┣━━━╋━━━╋━━━╋━━━╋━━━┫
//   ┃ * ┃   ┃   ┃   ┃   ┃
//   ┗━━━┻━━━┻━━━┻━━━┻━━━┛
//             S
// x座標は横マス（一番左が1，一番右がMaxX）
// y座標は縦マス（一番下が1，一番上がMaxY）
// * = 初期座標（1,1）

//*****************************************************************************
// Requirement
//*****************************************************************************

const LimitHeight = 3 //高度制限：(MaxHeight/2)+1とかしてもいいかも
const LimitX = 3 //移動制限：(LimitDestination/2)+1とかしてもいいかも
const LimitY = 3 //移動制限：(LimitDestination/2)+1とかしてもいいかも

fluent LOW_BATTERY = <battery['low],battery['safe]>
fluent FLYING = <takeoff,land>

//ltl_property P_DO_NOT_UP = [](!up[LimitHeight])
//ltl_property P_DO_NOT_MOVE_X = [](!move[LimitX][Y])
//ltl_property P_DO_NOT_MOVE_Y = [](!move[X][LimitY])
ltl_property P_LOW_BATTERY_UP   = [](LOW_BATTERY -> !up[2..MaxHeight])
ltl_property P_LOW_BATTERY_MOVE_X = [](LOW_BATTERY -> !move['e][2..MaxX][Y])
ltl_property P_LOW_BATTERY_MOVE_Y = [](LOW_BATTERY -> !move['n][X][2..MaxY])

//*****************************************************************************
// Controller
//*****************************************************************************

/* 従来の離散制御器合成 */
controllerSpec Specification =
{
	safety       = {
					P_LOW_BATTERY_UP,
					P_LOW_BATTERY_MOVE_X,
					P_LOW_BATTERY_MOVE_Y,
				   }
	//liveness     = {GOAL}
	controllable = {ControllerbleActions}
}
controller ||Controller = (DroneEnvironment)~{Specification}.
//controller ||Controller = (BATTERY || ALTITUDE || AREA)~{Specification}.


/* 段階的制御器合成（good case） */
controllerSpec SpecUP =
{
	safety       = {
					P_LOW_BATTERY_UP,
				   }
	//liveness     = {GOAL}
	controllable = {ControllerbleActions}
}

controllerSpec SpecMOVE =
{
	safety       = {
					P_LOW_BATTERY_MOVE_X,
					P_LOW_BATTERY_MOVE_Y,
				   }
	//liveness     = {GOAL}
	controllable = {ControllerbleActions}
}

||PartEnvironmentG1 = (BATTERY||ALTITUDE).
controller ||PartControllerG1 = (PartEnvironmentG1)~{SpecUP}.
||PartEnvironmentG2 = (PartControllerG1||AREA).
controller ||Controller_STEPWISED_GOOD = (PartEnvironmentG2)~{SpecMOVE}.

||PartEnvironmentB1 = (BATTERY||AREA).
controller ||PartControllerB1 = (PartEnvironmentB1)~{SpecMOVE}.
||PartEnvironmentB2 = (PartControllerB1||ALTITUDE).
controller ||Controller_STEPWISED_BAD = (PartEnvironmentB2)~{SpecUP}.

